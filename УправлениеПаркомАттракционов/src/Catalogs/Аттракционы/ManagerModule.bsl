#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Процедура ПеренестиФотоВПрисоединенныеФайлы(ПараметрыОбработки) Экспорт
	
	ПараметрыОбработки.ОбработкаЗавершена = Ложь;
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 50
	|	Аттракционы.Ссылка
	|ИЗ
	|	Справочник.Аттракционы КАК Аттракционы
	|ГДЕ
	|	Аттракционы.ФотоПеренесено = ЛОЖЬ";
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		
		ПараметрыОбработки.ОбработкаЗавершена = Истина;
		Возврат;
		
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			СпрОбъект = Выборка.Ссылка.ПолучитьОбъект();
			
			ДанныеФото = СпрОбъект.УдалитьФото.Получить();
			
			Если ДанныеФото <> Неопределено Тогда
				ПеренестиФотоВПрисоединенныйФайл(СпрОбъект, ДанныеФото);
			КонецЕсли;
			
			СпрОбъект.УдалитьФото = Неопределено;
			СпрОбъект.ФотоПеренесено = Истина;
			
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(СпрОбъект);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			ВызватьИсключение;
			
		КонецПопытки;
		
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПеренестиФотоВПрисоединенныйФайл(СпрОбъект, ДанныеФото)
	
	Адрес = ПоместитьВоВременноеХранилище(ДанныеФото);
	ПараметрыДобавления = РаботаСФайлами.ПараметрыДобавленияФайла();
	ПараметрыДобавления.ВладелецФайлов = СпрОбъект.Ссылка;
	ПараметрыДобавления.РасширениеБезТочки = СтрЗаменить(СпрОбъект.РасширениеФайлаФото, ".", "");
	ПараметрыДобавления.ИмяБезРасширения = "image";
	
	СпрОбъект.Фото = РаботаСФайлами.ДобавитьФайл(ПараметрыДобавления, Адрес); 
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли