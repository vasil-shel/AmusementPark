
#Область ОписаниеПеременных

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	АдресФото = ПолучитьНавигационнуюСсылку(ТекущийОбъект.Ссылка, "Фото");	

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ЭтоАдресВременногоХранилища(АдресФото) Тогда
		
		ТекущийОбъект.Фото = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(АдресФото));
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	Если ЭтоАдресВременногоХранилища(АдресФото) Тогда
		
		ПоместитьВоВременноеХранилище(Неопределено, АдресФото);
		АдресФото = ПолучитьНавигационнуюСсылку(Объект.Ссылка, "Фото");	
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура АдресФотоНажатие(Элемент, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	
	ВыбратьФото();	

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура ОчиститьФото(Команда)
	
	Если ЭтоАдресВременногоХранилища(АдресФото) Тогда
	
		ПоместитьВоВременноеХранилище(Неопределено, АдресФото);
		
	КонецЕсли;
	
	АдресФото = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Выбрать фото.
&НаКлиенте
Асинх Процедура ВыбратьФото()
	
	ПараметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов();
	ПараметрыДиалога.Фильтр = НСтр("ru = 'Картинка|*.jpg;*.jpeg;*.png'");
	ПараметрыДиалога.Заголовок = "Выбор картинки";
	
	Результат = Ждать ПоместитьФайлНаСерверАсинх(,,, ПараметрыДиалога, УникальныйИдентификатор);
	
	Если Результат = Неопределено 
		ИЛИ Результат.ПомещениеФайлаОтменено Тогда
		Возврат;
	КонецЕсли;
	
	АдресФото = Результат.Адрес;
	
КонецПроцедуры

#КонецОбласти
