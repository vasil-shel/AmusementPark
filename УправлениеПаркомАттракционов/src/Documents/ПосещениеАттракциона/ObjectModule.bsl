#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ,Режим)

	Движения.АктивныеПосещения.Записывать = Истина;
	Движения.Записать();
	
	РеквизитыАттракциона = РеквизитыАттракциона(Аттракцион);
	ВидАттракционаДокумента = РеквизитыАттракциона.ВидАттракциона;
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	|	АктивныеПосещенияОстатки.ВидАттракциона,
	|	АктивныеПосещенияОстатки.КоличествоПосещенийОстаток КАК КоличествоПосещений
	|ИЗ
	|	РегистрНакопления.АктивныеПосещения.Остатки(, Основание = &Основание
	|	И ВидАттракциона = &ВидАттракциона) КАК АктивныеПосещенияОстатки";
	Запрос.УстановитьПараметр("Основание", Основание);
	Запрос.УстановитьПараметр("ВидАттракциона", ВидАттракционаДокумента);
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		ОсталосьПосещений = 0;
		ВидАттракционаОсн = Справочники.ВидыАттракционов.ПустаяСсылка();
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ОсталосьПосещений = Выборка.КоличествоПосещений;
		ВидАттракционаОсн = Выборка.ВидАттракциона;
	КонецЕсли;
	
	Если ОсталосьПосещений < 1 Тогда
		Отказ = Истина;
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = "Не осталось посещений в билете.";
		Сообщение.УстановитьДанные(ЭтотОбъект);
		Сообщение.Поле = "Основание";
		Сообщение.Сообщить();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВидАттракционаОсн)
		И ВидАттракционаДокумента <> ВидАттракционаОсн Тогда
		Отказ = Истина;
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = "Билет не подходит для этого аттракциона.";
		Сообщение.УстановитьДанные(ЭтотОбъект);
		Сообщение.Поле = "Основание";
		Сообщение.Сообщить();
	КонецЕсли;
	
	Если Не Отказ Тогда
		// регистр АктивныеПосещения
		Движения.АктивныеПосещения.Записывать = Истина;
		Движение = Движения.АктивныеПосещения.Добавить();
		Движение.Период = Дата;
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Основание = Основание;
		Движение.ВидАттракциона = ВидАттракционаОсн;
		Движение.КоличествоПосещений = 1;
		
		// регистр ПопулярныеАттракционы
		Движения.ПосещенияАттракционов.Записывать = Истина;
		Движение = Движения.ПосещенияАттракционов.Добавить();
		Движение.Период = Дата;
		Движение.Аттракцион = Аттракцион;
		Движение.Клиент = Основание.Клиент;
		Движение.Количество = 1;
		
	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Ответственный = Пользователи.ТекущийПользователь();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция РеквизитыАттракциона(Аттракцион)
	Реквизиты = Новый Структура();
	Реквизиты.Вставить("ВидАттракциона", Справочники.ВидыАттракционов.ПустаяСсылка());

	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	|	Аттракционы.ВидАттракциона
	|ИЗ
	|	Справочник.Аттракционы КАК Аттракционы
	|ГДЕ
	|	Аттракционы.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", Аттракцион);
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(Реквизиты, Выборка);
	КонецЕсли;;
	
	Возврат Реквизиты;
		
КонецФункции

#КонецОбласти

#КонецЕсли
