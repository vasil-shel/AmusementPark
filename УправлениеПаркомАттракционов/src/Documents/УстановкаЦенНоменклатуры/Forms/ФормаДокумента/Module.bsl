#Область ОбработчикиСобытийФормы

// Код процедур и функций

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// Код процедур и функций

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Асинх Процедура ЗагрузитьИзExcel(Команда)
	
	Если Объект.ЦеныНоменклатуры.Количество() > 0 Тогда
		
		ТекстВопроса = НСтр("ru = 'В таблице уже введены данные. Продолжить?'");
		Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
		Если Ответ <> КодВозвратаДиалога.Да Тогда
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	ПараметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов();
	ПараметрыДиалога.Фильтр = НСтр("ru = 'Книга Excel|*.xlsx'");
	ПараметрыДиалога.Заголовок = "Выбор файла";
	
	Результат = Ждать ПоместитьФайлНаСерверАсинх(,,, ПараметрыДиалога);
	
	Если Результат = Неопределено 
		ИЛИ Результат.ПомещениеФайлаОтменено Тогда
		Возврат;
	КонецЕсли;
	
	ОбработатьФайлExcel(Результат.Адрес);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбработатьФайлExcel(Адрес)

	Объект.ЦеныНоменклатуры.Очистить();

	ИмяФайла = ПолучитьИмяВременногоФайла("xlsx");
	
	ДвДанные = ПолучитьИзВременногоХранилища(Адрес);
	ДвДанные.Записать(ИмяФайла);
	
	ТабДок = Новый ТабличныйДокумент();
	ТабДок.Прочитать(ИмяФайла);
	
	Для Сч = 2 По ТабДок.ВысотаТаблицы Цикл
		
		НоменклатураТекст = ТабДок.Область(Сч, 1).Текст;
		Номенклатура = Справочники.Номенклатура.НайтиПоНаименованию(НоменклатураТекст);
		
		Если Не ЗначениеЗаполнено(Номенклатура) Тогда
			Сообщение = Новый СообщениеПользователю();
			Сообщение.Текст = СтрШаблон("Не найдена номенклатура %1", НоменклатураТекст);
			Сообщение.Сообщить();
			Продолжить
		КонецЕсли;
		
		СтрокаТЧ = Объект.ЦеныНоменклатуры.Добавить();
		СтрокаТЧ.Номенклатура = Номенклатура;
		СтрокаТЧ.Цена = Число(ТабДок.Область(Сч, 2).Текст);
		
	КонецЦикла;
	
	УдалитьФайлы(ИмяФайла);
	
КонецПроцедуры

#КонецОбласти
