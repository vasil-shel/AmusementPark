
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ,Режим)

	// регистр ШкалаБонуснойПрограммы
	Движения.ШкалаБонуснойПрограммы.Записывать = Истина;
	Движение = Движения.ШкалаБонуснойПрограммы.Добавить();
	Движение.Период = Дата;
	Движение.Шкала = Ссылка;

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если Диапазоны.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если Диапазоны[0].НижняяГраница <> 0 Тогда
		Отказ = Истина;
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = "Нижняя граница первого диапазона должна быть равна 0";
		Сообщение.УстановитьДанные(ЭтотОбъект);
		Сообщение.Поле = "Диапазоны[0].НижняяГраница";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	КолДиапазонов = Диапазоны.Количество();
	
	Для Сч = 0 По КолДиапазонов-1 Цикл
		
		Если Сч < КолДиапазонов-1 Тогда
			
			Если Диапазоны[Сч].ВерхняяГраница <> Диапазоны[Сч+1].НижняяГраница Тогда
				Отказ = Истина;
				Сообщение = Новый СообщениеПользователю();
				Сообщение.Текст = "Верхняя граница диапазона должна быть равна нижней границе следующего диапазона";
				Сообщение.УстановитьДанные(ЭтотОбъект);
				Сообщение.Поле = СтрШаблон("Диапазоны[%1].ВерхняяГраница", Сч);
				Сообщение.Сообщить();
			КонецЕсли;
			
			Если Диапазоны[Сч].ВерхняяГраница <= Диапазоны[Сч].НижняяГраница Тогда
				Отказ = Истина;
				Сообщение = Новый СообщениеПользователю();
				Сообщение.Текст = "Верхняя граница диапазона должна быть больше нижней границы";
				Сообщение.УстановитьДанные(ЭтотОбъект);
				Сообщение.Поле = СтрШаблон("Диапазоны[%1].ВерхняяГраница", Сч);
				Сообщение.Сообщить();
			КонецЕсли;
			
		Иначе
			
			Если Диапазоны[Сч].ВерхняяГраница <> 0 Тогда
				Отказ = Истина;
				Сообщение = Новый СообщениеПользователю();
				Сообщение.Текст = "Верхняя граница последнего диапазона должна быть равна 0";
				Сообщение.УстановитьДанные(ЭтотОбъект);
				Сообщение.Поле = СтрШаблон("Диапазоны[%1].ВерхняяГраница", Сч);
				Сообщение.Сообщить();
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры


Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Ответственный = Пользователи.ТекущийПользователь();

КонецПроцедуры

#КонецОбласти

#КонецЕсли
