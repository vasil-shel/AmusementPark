
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Подготовить архив.
// 
// Возвращаемое значение:
//  Строка - Подготовить архив
Функция ПодготовитьАрхив() Экспорт
	
	ИмяКаталога = ПолучитьИмяВременногоФайла("");
	СоздатьКаталог(ИмяКаталога);
	Шаблон = "%1%2img%2%3%4";
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	|	ПРЕДСТАВЛЕНИЕ(УНИКАЛЬНЫЙИДЕНТИФИКАТОР(Аттракционы.Ссылка)) КАК ИД,
	|	Аттракционы.Представление КАК Аттракцион,
	|	Аттракционы.ВидАттракциона.Представление КАК ВидАттракциона,
	|	Аттракционы.Фото,
	|	ЕСТЬNULL(ЦеныНоменклатурыСрезПервых.Цена, 0) КАК Цена,
	|	Аттракционы.РасширениеФайлаФото
	|ИЗ
	|	Справочник.Аттракционы КАК Аттракционы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПервых КАК ЦеныНоменклатурыСрезПервых
	|		ПО Аттракционы.ВидАттракциона = ЦеныНоменклатурыСрезПервых.Номенклатура.ВидАттракциона
	|ГДЕ
	|	Аттракционы.ПометкаУдаления = ЛОЖЬ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Аттракцион";
	Выборка = Запрос.Выполнить().Выбрать();
	
	ОписаниеАттракционов = Новый Массив();

	Пока Выборка.Следующий() Цикл
		
		ОписаниеАттракциона = Новый Структура("ИД, Аттракцион, ВидАттракциона, Цена");
		ЗаполнитьЗначенияСвойств(ОписаниеАттракциона, Выборка);

		ДанныеФото = Выборка.Фото.Получить();
		
		Если ДанныеФото = Неопределено Тогда
			ОписаниеАттракциона.Вставить("Фото", Неопределено);
		Иначе
			ИмяФайла = СтрШаблон(Шаблон, ИмяКаталога, ПолучитьРазделительПути(), Выборка.ИД, Выборка.РасширениеФайлаФото);
			ДанныеФото.Записать(ИмяФайла);
			ИмяФайла = СтрШаблон(Шаблон, "", ПолучитьРазделительПути(), Выборка.ИД, Выборка.РасширениеФайлаФото);
			ОписаниеАттракциона.Вставить("Фото", ИмяФайла);			
			
		КонецЕсли;	
		
		ОписаниеАттракционов.Добавить(ОписаниеАттракциона);
		
	КонецЦикла;
	
	ИмяФайлаВыгрузки = СтрШаблон("%1%2export.json", ИмяКаталога, ПолучитьРазделительПути());
	Запись = Новый ЗаписьJSON();
	Запись.ОткрытьФайл(ИмяФайлаВыгрузки);
	ЗаписатьJSON(Запись, ОписаниеАттракционов);
	Запись.Закрыть();
	
	ИмяФайлаАрхива = СтрШаблон("%1%2*.*", ИмяКаталога, ПолучитьРазделительПути());
	Архиватор = Новый ЗаписьZipФайла();
	Архиватор.Добавить(ИмяФайлаАрхива, , РежимОбработкиПодкаталоговZIP.ОбрабатыватьРекурсивно);
	ДанныеАрхива = Архиватор.ПолучитьДвоичныеДанные();

	Адрес = ПоместитьВоВременноеХранилище(ДанныеАрхива, Новый УникальныйИдентификатор());
	УдалитьФайлы(ИмяКаталога);
	
	Возврат Адрес;
	
КонецФункции

#КонецОбласти

#КонецЕсли
