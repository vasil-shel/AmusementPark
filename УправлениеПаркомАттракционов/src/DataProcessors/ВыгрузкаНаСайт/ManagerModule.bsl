
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Подготовить архив.
// 
// Возвращаемое значение:
//  ДвоичныеДанные - ДвоичныеДанные - Подготовить архив
Функция ПодготовитьАрхив() Экспорт
	
	Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации("ВыгрузНаСайт");
	
	ИмяКаталога = ПолучитьИмяВременногоФайла("");
	СоздатьКаталог(ИмяКаталога);
	Шаблон = "%1%2img%2%3.%4";
	Шаблон1 = "%1%2.%3";
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	|	ПРЕДСТАВЛЕНИЕ(УНИКАЛЬНЫЙИДЕНТИФИКАТОР(Аттракционы.Ссылка)) КАК ИД,
	|	Аттракционы.Представление КАК Аттракцион,
	|	Аттракционы.ВидАттракциона.Представление КАК ВидАттракциона,
	|	ЕСТЬNULL(АттракционыПрисоединенныеФайлы.Ссылка, ЗНАЧЕНИЕ(Справочник.АттракционыПрисоединенныеФайлы.ПустаяСсылка)) КАК
	|		Фото,
	|	ЕСТЬNULL(ЦеныНоменклатурыСрезПервых.Цена, 0) КАК Цена,
	|	ЕСТЬNULL(АттракционыПрисоединенныеФайлы.Расширение, """") КАК РасширениеФайлаФото
	|ИЗ
	|	Справочник.Аттракционы КАК Аттракционы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПервых КАК ЦеныНоменклатурыСрезПервых
	|		ПО Аттракционы.ВидАттракциона = ЦеныНоменклатурыСрезПервых.Номенклатура.ВидАттракциона
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.АттракционыПрисоединенныеФайлы КАК АттракционыПрисоединенныеФайлы
	|		ПО Аттракционы.Ссылка = АттракционыПрисоединенныеФайлы.ВладелецФайла
	|ГДЕ
	|	Аттракционы.ПометкаУдаления = ЛОЖЬ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Аттракцион";
	Выборка = Запрос.Выполнить().Выбрать();
	
	ОценкаПроизводительности.ЗафиксироватьЗамерДлительнойОперации(Замер, Выборка.Количество(), "ВыполнениеЗапроса");
	
	ОписаниеАттракционов = Новый Массив();
	
	ДлительныеОперации.СообщитьПрогресс(33, НСтр("ru = 'Запущена выгрузка аттракционов'"));

	Пока Выборка.Следующий() Цикл
		
		ОписаниеАттракциона = Новый Структура("ИД, Аттракцион, ВидАттракциона, Цена");
		ЗаполнитьЗначенияСвойств(ОписаниеАттракциона, Выборка);
		ОписаниеАттракциона.Вставить("Фото", Неопределено);

		Если ЗначениеЗаполнено(Выборка.Фото) Тогда
			
			ДанныеФото = РаботаСФайлами.ДвоичныеДанныеФайла(Выборка.Фото);
			ИмяФайла = СтрШаблон(Шаблон, ИмяКаталога, ПолучитьРазделительПути(), Выборка.ИД, Выборка.РасширениеФайлаФото);
			ДанныеФото.Записать(ИмяФайла);
			ИмяФайла = СтрШаблон(Шаблон1, ПолучитьРазделительПути(), Выборка.ИД, Выборка.РасширениеФайлаФото);
			ОписаниеАттракциона.Вставить("Фото", ИмяФайла);			

		КонецЕсли;
		
		ОписаниеАттракционов.Добавить(ОписаниеАттракциона);
		
	КонецЦикла;
	
	ОценкаПроизводительности.ЗафиксироватьЗамерДлительнойОперации(Замер, Выборка.Количество(), "СформированКаталог");

	ИмяФайлаВыгрузки = СтрШаблон("%1%2export.json", ИмяКаталога, ПолучитьРазделительПути());
	Запись = Новый ЗаписьJSON();
	Запись.ОткрытьФайл(ИмяФайлаВыгрузки);
	ЗаписатьJSON(Запись, ОписаниеАттракционов);
	Запись.Закрыть();

	ДлительныеОперации.СообщитьПрогресс(66, НСтр("ru = 'Формируем архив...'"));
	
	ИмяФайлаАрхива = СтрШаблон("%1%2*.*", ИмяКаталога, ПолучитьРазделительПути());
	Архиватор = Новый ЗаписьZipФайла();
	Архиватор.Добавить(ИмяФайлаАрхива, , РежимОбработкиПодкаталоговZIP.ОбрабатыватьРекурсивно);
	ДанныеАрхива = Архиватор.ПолучитьДвоичныеДанные();

	ОценкаПроизводительности.ЗафиксироватьЗамерДлительнойОперации(Замер, Выборка.Количество(), "СформированАрхив");

	УдалитьФайлы(ИмяКаталога);
	
	ОценкаПроизводительности.ЗафиксироватьЗамерДлительнойОперации(Замер, Выборка.Количество(), "УдаленыВременныеФайлы");
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(Замер, Выборка.Количество());
	
	Возврат ДанныеАрхива;
	
КонецФункции

#КонецОбласти

#КонецЕсли
