
#Область ПрограммныйИнтерфейс

// Перевести пользователей из предыдущей версии.
Процедура ПеревестиПользователейИзПредыдущейВерсии() Экспорт
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	|	па_Пользователи.Ссылка,
	|	па_Пользователи.Наименование,
	|	па_Пользователи.ИдентиФикаторПользователяИБ
	|ИЗ
	|	Справочник.па_Пользователи КАК па_Пользователи
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
	|		ПО па_Пользователи.ИдентиФикаторПользователяИБ = Пользователи.ИдентификаторПользователяИБ
	|ГДЕ
	|	Пользователи.Ссылка ЕСТЬ NULL
	|	И па_Пользователи.ИдентиФикаторПользователяИБ <> &ПустойИД";	
	Запрос.УстановитьПараметр("ПустойИД", ОбщегоНазначенияКлиентСервер.ПустойУникальныйИдентификатор());
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			ПользовательИБ = ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(Выборка.ИдентификаторПользователяИБ);
	
			СпрОбъект = Справочники.Пользователи.СоздатьЭлемент();
			СпрОбъект.Наименование = Выборка.Наименование;
			
			ОписаниеПользователяИБ = Новый Структура;
			ОписаниеПользователяИБ.Вставить("Действие", "Записать");
			ОписаниеПользователяИБ.Вставить("УникальныйИдентификатор", Выборка.ИдентификаторПользователяИБ);
			СпрОбъект.ДополнительныеСвойства.Вставить("ОписаниеПользователяИБ", ОписаниеПользователяИБ);
			
			СпрОбъект.Записать();
			
			НазначитьПрофилиГруппДоступа(СпрОбъект.Ссылка, ПользовательИБ);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			ВызватьИсключение;
			
		КонецПопытки;
					
	КонецЦикла;
	
КонецПроцедуры

// Вызывается при переходе на версию конфигурации 3.1.10.74
// Актуализирует предопределенные виды контактной информации:
// - обновляет данные реквизитов на основании данных из процедуры ПриНачальномЗаполненииЭлементов;
// - не обновляет реквизиты, которые были изменены пользователем;
// - добавляет новые виды контактной информации, если они отсутствуют в информационной базе.
// - обновляет только перечисленные реквизиты и виды контактной информации
//
Процедура АктуализироватьВидыКонтактнойИнформации() Экспорт
	
	ПараметрыОбновления = ОбновлениеИнформационнойБазы.ПараметрыОбновленияПредопределенныхЭлементов();
	ПараметрыОбновления.Реквизиты = "РазрешитьВводНесколькихЗначений, ВидРедактирования, ОтображатьВсегда, Используется";
	ОбновлениеИнформационнойБазы.ОбновитьПредопределенныеЭлементы(Метаданные.Справочники.ВидыКонтактнойИнформации, ПараметрыОбновления);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура НазначитьПрофилиГруппДоступа(Пользователь, ПользовательИБ)
	
	Если ПользовательИБ = Неопределено Тогда
		
		Возврат;

	КонецЕсли;
	
	ЭтоАдминистратор = Ложь;
	ЭтоКассир = Ложь;
	ЭтоОператор = Ложь;
	
	Для Каждого Роль Из ПользовательИБ.Роли Цикл
		
		Если Роль = Метаданные.Роли.па_ПолныеПрава Тогда
			
			ЭтоАдминистратор = Истина;
			
		ИначеЕсли Роль = Метаданные.Роли.Кассир Тогда
			
			ЭтоКассир = Истина;
			
		ИначеЕсли Роль = Метаданные.Роли.ОператорАттракциона Тогда
			
			ЭтоОператор = Истина;
			
		КонецЕсли;
		
	КонецЦикла; 
		
	Если ЭтоАдминистратор Тогда
		
		УправлениеДоступом.ВключитьПрофильПользователю(Пользователь, "Администратор");
		
	ИначеЕсли ЭтоКассир Тогда
		
		УправлениеДоступом.ВключитьПрофильПользователю(Пользователь, "Кассир");
		
	ИначеЕсли ЭтоОператор Тогда
		
		УправлениеДоступом.ВключитьПрофильПользователю(Пользователь, "ОператорАттракциона");
		
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти
