#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Период.Вариант = ВариантСтандартногоПериода.ЭтотМесяц;
	СформироватьДиаграммы();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	
	СформироватьДиаграммы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПосещениеАттракционовВыбор(Элемент, ЗначениеДиаграммы, СтандартнаяОбработка)
	
	Аттракцион = ЗначениеДиаграммы.Серия.Значение;
	День = ЗначениеДиаграммы.Точка.Значение;
	
	ПользНастройки = НастройкиОтчетаДетализацияПосещений(Аттракцион, День);
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ПользовательскиеНастройки", ПользНастройки);
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	
	ОткрытьФорму("Отчет.ДетализацияПосещений.Форма", ПараметрыФормы);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Обновить(Команда)
	
	СформироватьДиаграммы();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СформироватьДиаграммы()
	СформироватьДиаграммуПосещенияАттракционов();
	СформироватьДиаграммуРаспределениеАттракционов();
КонецПроцедуры

&НаСервере
Процедура СформироватьДиаграммуПосещенияАттракционов()
	
	ПосещениеАттракционов.Очистить();
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	|	ПосещенияАттракционовОбороты.Период КАК Период,
	|	ПосещенияАттракционовОбороты.Аттракцион КАК Аттракцион,
	|	ПосещенияАттракционовОбороты.КоличествоОборот КАК Количество
	|ИЗ
	|	РегистрНакопления.ПосещенияАттракционов.Обороты(&ДатаН, &ДатаК, День,) КАК ПосещенияАттракционовОбороты
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период,
	|	Аттракцион";
	Запрос.УстановитьПараметр("ДатаН", Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаК", Период.ДатаОкончания);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Точка = ПосещениеАттракционов.УстановитьТочку(Выборка.Период);
		Точка.Текст = Формат(Выборка.Период, "ДЛФ=D;");
		Серия = ПосещениеАттракционов.УстановитьСерию(Выборка.Аттракцион);
		
		ПосещениеАттракционов.УстановитьЗначение(Точка, Серия, Выборка.Количество);
				
	КонецЦикла;

	ВремяОбновления = ТекущаяДатаСеанса();
	
КонецПроцедуры

&НаСервере
Процедура СформироватьДиаграммуРаспределениеАттракционов()
	
	РаспределениеАттракционов.Очистить();
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	|	ПосещенияАттракционовОбороты.Аттракцион КАК Аттракцион,
	|	ПосещенияАттракционовОбороты.КоличествоОборот КАК Количество,
	|	ПосещенияАттракционовОбороты.Аттракцион.ВидАттракциона КАК ВидАттракциона
	|ИЗ
	|	РегистрНакопления.ПосещенияАттракционов.Обороты(&ДатаН, &ДатаК,,) КАК ПосещенияАттракционовОбороты
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВидАттракциона,
	|	Аттракцион
	|ИТОГИ
	|	СУММА(Количество)
	|ПО
	|	ВидАттракциона";
	Запрос.УстановитьПараметр("ДатаН", Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаК", Период.ДатаОкончания);
	ВыборкаВид = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаВид.Следующий() Цикл
		
		Точка = РаспределениеАттракционов.УстановитьТочку("Виды аттракционов");
		Серия = РаспределениеАттракционов.УстановитьСерию(ВыборкаВид.ВидАттракциона);
		
		РаспределениеАттракционов.УстановитьЗначение(Точка, Серия, ВыборкаВид.Количество);
		
		Выборка = ВыборкаВид.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
		Точка = РаспределениеАттракционов.УстановитьТочку("Аттракционы");
		Серия = РаспределениеАттракционов.УстановитьСерию(Выборка.Аттракцион);
		
		РаспределениеАттракционов.УстановитьЗначение(Точка, Серия, Выборка.Количество);
			
		КонецЦикла;
				
	КонецЦикла;

	ВремяОбновления = ТекущаяДатаСеанса();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НастройкиОтчетаДетализацияПосещений(Аттракцион, День)
	
	Отчет = Отчеты.ДетализацияПосещений.Создать();
	Настройки = Отчет.КомпоновщикНастроек.Настройки;
	ПользовательскиеНастройки = Отчет.КомпоновщикНастроек.ПользовательскиеНастройки;
	
	// Период
	ПараметрПериод = Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Период"));
	ПользПараметрПериод = ПользовательскиеНастройки.Элементы.Найти(ПараметрПериод.ИдентификаторПользовательскойНастройки);
	
	ЗначениеПараметрыПериод = Новый СтандартныйПериод();
	ЗначениеПараметрыПериод.ДатаНачала = День;
	ЗначениеПараметрыПериод.ДатаОкончания = День;
	
	ПользПараметрПериод.Значение = ЗначениеПараметрыПериод;
	
	// Отбор Аттракцион
	ПолеОтбора = Новый ПолеКомпоновкиДанных("Аттракцион");
	
	Для Каждого ЭлементОтбора Из Настройки.Отбор.Элементы Цикл
		
		Если ЭлементОтбора.ЛевоеЗначение = ПолеОтбора Тогда
			
			ПользПараметрАттракцион = ПользовательскиеНастройки.Элементы.Найти(ЭлементОтбора.ИдентификаторПользовательскойНастройки);
			ПользПараметрАттракцион.Использование = Истина;
			ПользПараметрАттракцион.ПравоеЗначение = Аттракцион;
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
		
	Возврат ПользовательскиеНастройки;
	
КонецФункции

#КонецОбласти
